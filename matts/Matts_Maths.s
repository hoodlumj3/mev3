
; Divide 2 long words

_CXD22:
	MOVE.L	D2,-(A7)
	SWAP	D1
	MOVE.W	D1,D2
	BNE	.10
	SWAP	D0
	SWAP	D1
	SWAP	D2
	MOVE.W	D0,D2
	BEQ	.20
	DIVU	D1,D2
	MOVE.W	D2,D0
.20	SWAP	D0
	MOVE.W	D0,D2
	DIVU	D1,D2
	MOVE.W	D2,D0
	SWAP	D2
	MOVE.W	D2,D1
	MOVE.L	(A7)+,D2
	RTS
.10	MOVE.L	D3,-(A7)
	MOVEQ	#$10,D3
	CMPI.W	#$80,D1
	BCC	.30
	ROL.L	#8,D1
	SUBQ.W	#8,D3
.30	CMPI.W	#$800,D1
	BCC	.40
	ROL.L	#4,D1
	SUBQ.W	#4,D3
.40	CMPI.W	#$2000,D1
	BCC	.50
	ROL.L	#2,D1
	SUBQ.W	#2,D3
.50	TST.W	D1
	BMI	.60
	ROL.L	#1,D1
	SUBQ.W	#1,D3
.60	MOVE.W	D0,D2
	LSR.L	D3,D0
	SWAP	D2
	CLR.W	D2
	LSR.L	D3,D2
	SWAP	D3
	DIVU	D1,D0
	MOVE.W	D0,D3
	MOVE.W	D2,D0
	MOVE.W	D3,D2
	SWAP	D1
	MULU	D1,D2
	SUB.L	D2,D0
	BCC	.70
	SUBQ.W	#1,D3
	ADD.L	D1,D0
.70	MOVEQ	#0,D1
	MOVE.W	D3,D1
	SWAP	D3
	ROL.L	D3,D0
	SWAP	D0
	EXG	D0,D1
	MOVE.L	(A7)+,D3
	MOVE.L	(A7)+,D2
	RTS

; Multiply 2 long words

_CXM22:
	MOVEM.L	D1-3,-(A7)
	MOVE.L	D0,D2
	BEQ.S	.100
	TST.L	D1
	BNE.S	.50
	CLR.L	D0
	BRA.S	.100
.50
	MOVE.L	D0,D3
	MULU	D1,D3
	SWAP	D2
	MULU	D1,D2
	SWAP	D2
	CLR.W	D2
	ADD.L	D2,D3
	SWAP	D1
	MULU	D1,D0
	SWAP	D0
	CLR.W	D0
	ADD.L	D3,D0
.100
	MOVEM.L	(A7)+,D1-3
	RTS

